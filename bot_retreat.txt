
register DO_HUNT

sync
	register _MOVE_LEFT
	register _MOVE_RIGHT
	register _MOVE_LITTLE
	register _DIST
	
	iterate forever
	
		when DO_HUNT is true
			when BOT_X_UNKNOWN is false
				when BOT_POS.x is greater than 40
					assign _MOVE_LEFT true
					assign _DIST computing BOT_POS.x sub 40
					when _DIST is less than 5
						assign _MOVE_LITTLE true
					otherwise
						assign _MOVE_LITTLE false
					end
				otherwise
					assign _MOVE_LEFT false
				end
				
				when BOT_POS.x is less than 37
					assign _MOVE_RIGHT true
					assign _DIST computing 37 sub BOT_POS.x
					when _DIST is less than 5
						assign _MOVE_LITTLE true
					otherwise
						assign _MOVE_LITTLE false
					end				
				otherwise
					assign _MOVE_RIGHT false
				end
			end
			
			when _MOVE_LEFT is true
				when _MOVE_LITTLE is true
					key left for 200
				otherwise
					key left for 600
				end
			otherwise
				when _MOVE_RIGHT is true
					when _MOVE_LITTLE is true
						key right for 200
					otherwise
						key right for 600
					end
				otherwise
					assign _RANDOM random over 100
					when _RANDOM is less than 50
						key left for 100
					otherwise
						key right for 100
					end
					key up for 200
					
					sprite find _SPR for 15 using GWI_BOT over 0 0 50 60
					when _SPR.x is less than 0
						iterate forever
							import bot_settings.txt CHECK_MAPNAME
							when MAPNAME_TEST is true
								finish iterate
							end			
							delay for DELAY_MONITOR
						end
						delay for 1 sec
						
						assign DO_HUNT false
						
						# Change routine
						assign ROUTINE 0
						finish sync
					end
				end
			end
		end
			
		delay for 500
	end
	
with
	iterate forever

		assign _NOW time

		when RETREAT_TIMER.out is unequal to 0
			when _NOW is greater than RETREAT_TIMER.out
				assign RETREAT_TIMER.out 0				
				assign DO_HUNT true
			end
		end

		delay for DELAY_CHECK
	end

with
	register _VIOLETTA_COUNT
	register _TEMP
	
	iterate forever

		import bot_settings.txt CHECK_VIOLETTA
		when VIOLETTA_TEST is true
			increase _VIOLETTA_COUNT
			when _VIOLETTA_COUNT is equal to 1
				assign RETREAT_TIMER.in time later RETREAT_CYCLE.period
				assign RETREAT_TIMER.in computing RETREAT_TIMER.in sub RETREAT_CYCLE.enter
				
				assign RETREAT_TIMER.out time later 45 sec
			end
			sound boing
			delay for 500
		otherwise
			assign _VIOLETTA_COUNT 0
		end
		
		delay for DELAY_CHECK
	end
end
